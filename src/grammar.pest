// It's important to add SOI and EOI to tell the parser to validate the whole input.
// Otherwise it may silently fail somewhere in the middle when it encounters and invalid input
TestSuite = { SOI ~ (NEWLINE|COMMENT)* ~ TestCase* ~ EOL}

COMMENT = _{ ("#" ~ (!NEWLINE ~ ANY)* ~ EOL) }

TestCase = { TestCaseHeader ~ Instructions }

TestCaseHeader = _{ HeaderOpen ~ TestCaseName ~ HeaderClose ~ EOL }
TestCaseName = ${ (!HeaderClose ~ !EOL ~ ANY)+ }
HeaderOpen = _{ "[" }
HeaderClose = _{ "]" }

Instructions = _{ (InstructionWithPayload | InstructionControlChar | InstructionExitCode)+ }

InstructionWithPayload = { ProcessNumber? ~ InstructionIdentifier ~ Space ~ Payload ~ EOL}
Payload = ${ (!NEWLINE ~ ANY)* ~ &EOL }

InstructionExitCode = { ProcessNumber? ~ IdentifierExitCode ~ Space ~ ExitCode ~ EOL}
ExitCode = { ASCII_DIGIT+ ~ &EOL}

InstructionControlChar = { ProcessNumber? ~ IdentifierControlChar ~ Space ~ ControlChar ~ EOL}
ControlChar = { ASCII ~ &EOL}

InstructionIdentifier = { (IdentifierLaunch | IdentifierStdin | IdentifierStdout | IdentifierRegex | IdentifierExitCode) }
IdentifierLaunch = { "$" }
IdentifierStdin = { "<" }
IdentifierStdout = { ">" }
IdentifierRegex = { "r" }
IdentifierControlChar = { "^" }
IdentifierExitCode = { "?" }

ProcessNumber = { ASCII_DIGIT }

EOL = _{ NEWLINE+ | &EOI}
Space = _{ " " }